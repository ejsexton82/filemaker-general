/**
 * ==================================================================
 * GetPlugin ( name )
 *
 * PARAMETERS:
 *            @name (text) Plug-in name
 * RETURNS:
 *            (text) Installed plug-in value
 *            $pluginExists  (bool) TRUE if the plug-in exists
 *            $pluginVersion (text) Plug-in version string
 *            $pluginEnabled (bool) TRUE if the plug-in is enabled
 * DEPENDENCIES:
 *            none
 * PURPOSE:
 *            Retrieves information about an installed plug-in,
 *            and parses the information into variables.
 * NOTES:
 *            none
 * REVISIONS:
 *            2018-07-17 - Initial implementation.
 *            2018-07-24 - Fixed partial matches and no matches.
 * ==================================================================
 */

Let ( [
	~plugin = name ;
	~plugins = Get ( InstalledFMPlugins ) ;
	~startPos = Position ( "¶" & ~plugins ; "¶" & ~plugin & ";" ; 1 ; 1 ) ;
	$pluginExists = ~startPos > 0 ;
	~nextLinePos = If ( $pluginExists ; Position ( ~plugins ; "¶" ; ~startPos ; 1 ) ) ;
	~endPos = If ( $pluginExists ; If ( ~nextLinePos ; ~nextLinePos ; Length ( ~plugins ) ) ) ;
	~line = If ( $pluginExists ; Middle ( ~plugins ; ~startPos ; 1 + ~endPos - ~startPos ) ) ;
	~values = Substitute ( ~line ; ";" ; "¶" ) ;
	$pluginVersion = GetValue ( ~values ; 2 ) ;
	$pluginEnabled = GetValue ( ~values ; 3 ) = "Enabled"
] ;
	~line
)
