/**
 * ==================================================================
 * GetPlugin ( name )
 *
 * PARAMETERS:
 *            @name (text) Plug-in name
 * RETURNS:
 *            (text) Installed plug-in value
 *            $pluginExists  (bool) TRUE if the plug-in exists
 *            $pluginVersion (text) Plug-in version string
 *            $pluginEnabled (bool) TRUE if the plug-in is enabled
 * DEPENDENCIES:
 *            none
 * PURPOSE:
 *            Retrieve information about an installed plug-in,
 *            and parses the information into variables.
 * NOTES:
 *            This function includes additional parsing that may be useful.
 * REVISIONS:
 *            2018-07-17 - Initial implementation.
 * ==================================================================
 */

Let ( [
    ~plugin = name ;
    ~plugins = Get ( InstalledFMPlugins ) ;
    ~startPos = Position ( ~plugins ; ~plugin ; 1 ; 1 ) ;
    $pluginExists = ~startPos > 0 ;
    ~nextLinePos = If ( $pluginExists ; Position ( ~plugins ; "¶" ; ~startPos ; 1 ) ) ;
    ~endPos = If ( $pluginExists ; If ( ~nextLinePos ; ~nextLinePos ; Length ( ~plugins ) ) ) ;
    ~line = Middle ( ~plugins ; ~startPos ; 1 + ~endPos - ~startPos ) ;
    ~values = Substitute ( ~line ; ";" ; "¶" ) ;
    $pluginVersion = GetValue ( ~values ; 2 ) ;
    $pluginEnabled = GetValue ( ~values ; 3 ) = "Enabled"
] ;
    ~line
)
